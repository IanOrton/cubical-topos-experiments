----------------------------------------------------------------------
-- This Agda code is designed to accompany the paper "Axioms for
-- Modelling Cubical Type Theory in a Topos" (CSL Special Issue
-- version). 
--
-- The idea for getting an impredicative universe of propositions Î©
-- comes from Martin Escardo, more details can be found at:
--
--          http://www.cs.bham.ac.uk/~mhe/impredicativity/          
----------------------------------------------------------------------

{-# OPTIONS --rewriting #-}
module replace where 

open import Agda.Builtin.TrustMe
open import prelude
open import impredicative
open import interval
open import cof
open import fibrations


----------------------------------------------------------------------
-- Lemmas about subst
----------------------------------------------------------------------
substcancel :
  {â„“ â„“' : Level}
  {A : Set â„“}
  (B : A â†’ Set â„“')
  {x y : A}
  (f : (a : A) â†’ B a)
  (p : x â‰¡ y)
  {z : A}
  (q : x â‰¡ z)
  (r : y â‰¡ z)
  â†’ ------------------------
  subst B q (f x) â‰¡ subst B r (f y)
substcancel _ _ refl refl refl = refl

substtrans :
  {â„“ â„“' : Level}
  {A : Set â„“}
  (B : A â†’ Set â„“')
  {x y z : A}
  (p : x â‰¡ y)
  (q : y â‰¡ z)
  (r : x â‰¡ z)
  (b : B x)
  â†’ ------------------------
  subst B q (subst B p b)  â‰¡ subst B r b
substtrans _ refl refl refl _ = refl

substcong :
  {â„“ â„“' : Level}
  {A A' : Set â„“}
  (B : A' â†’ Set â„“')
  (f : A â†’ A')
  {x y : A}
  (p : x â‰¡ y)
  (b : B (f x))
  â†’ ------------------------
  subst (B âˆ˜ f) p b â‰¡ subst B (cong f p) b
substcong _ _ refl _ = refl

----------------------------------------------------------------------
-- Quoitents
----------------------------------------------------------------------
infix 6 _/_ [_]/_
postulate
  -- quotient formation
  _/_ : {â„“ â„“' : Level}(A : Set â„“)(R : A â†’ A â†’ Set â„“') â†’ Set (â„“ âŠ” â„“')
  
  -- quotient introduction
  [_]/_ : {â„“ â„“' : Level}{A : Set â„“} â†’ A â†’ (R : A â†’ A â†’ Set â„“') â†’ A / R

  -- generating equalities
  qeq :
    {â„“ â„“' : Level}
    {A : Set â„“}
    {x y : A}
    (R : A â†’ A â†’ Set â„“')
    (r : R x y)
    â†’ ------------------
    [ x ]/ R â‰¡ [ y ]/ R

  -- quotient induction
  qind :
    {â„“ â„“' â„“'' : Level}
    {A : Set â„“}
    (R : A â†’ A â†’ Set â„“')
    (B : A / R â†’ Set â„“'')
    (f : (x : A) â†’ B ([ x ]/ R))
    (e : (x y : A)(r : R x y) â†’ subst B (qeq R r) (f x) â‰¡ f y)
    â†’ --------------------------------------------------------
    (y : A / R) â†’ B y

  -- quotient computation
  qind-comp :
    {â„“ â„“' â„“'' : Level}
    {A : Set â„“}
    (R : A â†’ A â†’ Set â„“')
    (B : A / R â†’ Set â„“'')
    (f : (x : A) â†’ B ([ x ]/ R))
    (e : (x y : A)(r : R x y) â†’ subst B (qeq R r) (f x) â‰¡ f y)
    (x : A)
    â†’ --------------------------------------------------------
    qind R B f e ([ x ]/ R) â‰¡ f x

{-# REWRITE qind-comp   #-}

-- N.B. Not sure if these are correct:
{-# POLARITY _/_ * * ++ ++ #-}
{-# POLARITY [_]/_ * * _ * _ #-}

----------------------------------------------------------------------
-- Fibrant replacement of an object
----------------------------------------------------------------------
isFibObj : Set â†’ Set
isFibObj A = isFib {Î“ = Unit} (Î» _ â†’ A)

mutual
  -- FreeComps A is the result of freely adding compositions to A
  data FreeComps (A : Set) : Set where
    pure : A â†’ FreeComps A
    comp :
      (e : OI)
      (Ï† : Cof)
      (f : [ Ï† ] â†’ Int â†’ Replace A)
      (aâ‚€ : âŸ¦ aâ‚€ âˆˆ Replace A âˆ£ (Ï† , f) âˆ™ âŸ¨ e âŸ© â†— aâ‚€ âŸ§)
      â†’ FreeComps A

  -- Replace A is FreeComps A where we quotient by the equation:
  --     comp e cofTrue f aâ‚€ = f tt
  -- i.e. we ensure that compositions always extend their partial input
  Replace : Set â†’ Set
  Replace A = FreeComps A / TotalComps

  -- TotalComps is the inductive family describing the necessary relation
  data TotalComps {A : Set} : FreeComps A â†’ FreeComps A â†’ Set where
    total :
      (e : OI)
      (Ï† : Cof)
      (f : [ Ï† ] â†’ Int â†’ Replace A)
      (aâ‚€ : âŸ¦ aâ‚€ âˆˆ Replace A âˆ£ (Ï† , f) âˆ™ âŸ¨ e âŸ© â†— aâ‚€ âŸ§)
      (u : [ Ï† ])
      (a : FreeComps A)
      (_ : [ a ]/ TotalComps â‰¡ f u âŸ¨ ! e âŸ©)
      â†’ TotalComps a (comp e Ï† f aâ‚€)

-- The inclusion of A in Replace A
Î¹ : {A : Set} â†’ A â†’ Replace A
Î¹ a = [ pure a ]/ TotalComps

-- Replace A is always a fibrant object
replaceIsFib : (A : Set) â†’ isFibObj (Replace A)
replaceIsFib A e p Ï† f aâ‚€ = [ comp e Ï† f aâ‚€ ]/ TotalComps , ext where
  ext : (u : [ Ï† ]) â†’ f u âŸ¨ ! e âŸ© â‰¡ [ comp e Ï† f aâ‚€ ]/ TotalComps
  ext u = qind TotalComps
    (Î» a â†’ (_ : a â‰¡ f u âŸ¨ ! e âŸ©) â†’ a â‰¡ [ comp e Ï† f aâ‚€ ]/ TotalComps)
    (Î» a p â†’ qeq TotalComps (total e Ï† f aâ‚€ u a p))
    (Î» _ _ _ â†’ funext (Î» _ â†’ uip _ _))
    (f u âŸ¨ ! e âŸ©) refl

-- We get a principle for eliminating into fibrant objects
replaceElim :
  (A : Set)
  (B : Set)(Î² : isFibObj B)
  (f : A â†’ B)
  â†’ --------------------
  Replace A â†’ B

-- We need to mark this as terminating, but this should (hopefully) be ok
{-# TERMINATING #-}
replaceElim A B Î² f = elim where
  elim : Replace A â†’ B
  f' : (x : FreeComps A) â†’ B
  resp : (x y : FreeComps A) (r : TotalComps x y) â†’ subst (Î» _ â†’ B) (qeq TotalComps r) (f' x) â‰¡ f' y
  elim = qind TotalComps (Î» _ â†’ B) f' resp
  f' (pure x) = f x
  f' (comp e Ï† g (aâ‚€ , ext)) = fst (Î² e (Î» _ â†’ tt) Ï† (Î» u i â†’ elim (g u i))
    (elim aâ‚€ , Î» u â†’ cong (qind TotalComps (Î» _ â†’ B) f' resp) (ext u)))
  resp a .(comp e Ï† g (aâ‚€ , ext)) (total e Ï† g (aâ‚€ , ext) u .a eq) =
    proof:
      subst (Î» _ â†’ B) (qeq TotalComps (total e Ï† g (aâ‚€ , ext) u a eq)) (f' a)
        â‰¡[ substconst _ (qeq TotalComps (total e Ï† g (aâ‚€ , ext) u a eq)) (f' a) ]â‰¡
      f' a
        â‰¡[ cong elim eq ]â‰¡
      elim (g u âŸ¨ ! e âŸ©)
        â‰¡[ snd (Î² e (Î» _ â†’ tt) Ï† (Î» u i â†’ elim (g u i))
             (elim aâ‚€ , Î» u â†’ cong (qind TotalComps (Î» _ â†’ B) f' resp) (ext u))) u ]â‰¡
      fst (Î² e (Î» _ â†’ tt) Ï† (Î» u i â†’ elim (g u i))
        (elim aâ‚€ , Î» u â†’ cong (qind TotalComps (Î» _ â†’ B) f' resp) (ext u)))
        â‰¡[ refl ]â‰¡
      f' (comp e Ï† g (aâ‚€ , ext))
    qed

-- Every f : A --> B factors as (replaceElim f) âˆ˜ Î¹
--
--  A --------> B
--   \         âš
--    \       /
--     \    /
--      â˜ /
--   Replace A
--
replaceElimFactors :
  (A : Set)
  (B : Set)(Î² : isFibObj B)
  (f : A â†’ B)
  â†’ ---------------
  (replaceElim A B Î² f) âˆ˜ Î¹ â‰¡ f
replaceElimFactors A B Î² f = refl 


-- A corresponding induction principle
replaceInd :
  (A : Set)
  (B : Replace A â†’ Set)(Î² : isFib B)
  (f : (a : A) â†’ B (Î¹ a))
  â†’ --------------------
  (x : Replace A) â†’ B x
{-# TERMINATING #-}
replaceInd A B Î² f = elim where
  elim : (x : Replace A) â†’ B x
  f' : (x : FreeComps A) â†’ B ([ x ]/ TotalComps)
  resp : (x y : FreeComps A) (r : TotalComps x y) â†’ subst B (qeq TotalComps r) (f' x) â‰¡ f' y
  elim = qind TotalComps B f' resp
  f' (pure x) = f x
  f' (comp e Ï† g (aâ‚€ , ext)) =
    let p = fill e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext in
    let g' u i = subst B (appCong (fst (snd p) u)) (elim (g u i)) in
    let aâ‚€' = subst B (symm (snd (snd p))) (elim aâ‚€) in
    let ext' u = substcancel B elim (ext u) (appCong (fst (snd p) u)) (symm (snd (snd p))) in
    let aâ‚' = fst (Î² e (fst p) Ï† g' (aâ‚€' , ext')) in
    subst B (fillAtEnd e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext) aâ‚'
  resp a .(comp e Ï† g (aâ‚€ , ext)) (total e Ï† g (aâ‚€ , ext) u .a eq) =
    let p = fill e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext in
    let g' u i = subst B (appCong (fst (snd p) u)) (elim (g u i)) in
    let aâ‚€' = subst B (symm (snd (snd p))) (elim aâ‚€) in
    let ext' u = substcancel B elim (ext u) (appCong (fst (snd p) u)) (symm (snd (snd p))) in
    let aâ‚' = fst (Î² e (fst p) Ï† g' (aâ‚€' , ext')) in
    proof:

      subst B (qeq TotalComps (total e Ï† g (aâ‚€ , ext) u a eq)) (f' a)

          â‰¡[ substcancel B elim eq
               (qeq TotalComps (total e Ï† g (aâ‚€ , ext) u a eq))
               (snd (replaceIsFib A e (Î» _ â†’ tt) Ï† g (aâ‚€ , ext)) u) ]â‰¡

      subst B (snd (replaceIsFib A e (Î» _ â†’ tt) Ï† g (aâ‚€ , ext)) u) (elim (g u âŸ¨ ! e âŸ©))

          â‰¡[ symm (substtrans B
                    (appCong (fst (snd p) u))
                    (fillAtEnd e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext)
                    (snd (replaceIsFib A e (Î» _ â†’ tt) Ï† g (aâ‚€ , ext)) u)
                    (elim (g u âŸ¨ ! e âŸ©))) ]â‰¡

      subst B (fillAtEnd e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext) (g' u âŸ¨ ! e âŸ©)
   
          â‰¡[ cong (subst B (fillAtEnd e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext))
                  (snd (Î² e (fst p) Ï† g' (aâ‚€' , ext')) u) ]â‰¡

      subst B (fillAtEnd e (replaceIsFib A) (Î» _ â†’ tt) Ï† g aâ‚€ ext) aâ‚'

          â‰¡[ refl ]â‰¡

      f' (comp e Ï† g (aâ‚€ , ext))

    qed


-- And a corresponding factorisation lemma
replaceIndFactors : (A : Set)(B : Replace A â†’ Set)(Î² : isFib B)(f : (a : A) â†’ B (Î¹ a))
  â†’ (Î» a â†’ replaceInd A B Î² f (Î¹ a)) â‰¡ f
replaceIndFactors A B Î² f = refl 

----------------------
-- ğ•ŠÂ¹
----------------------
open import Data.paths

data Endpoints : Int â†’ Int â†’ Set where
  I=O : Endpoints I O

preğ•ŠÂ¹ : Set
preğ•ŠÂ¹ = Int / Endpoints

ğ•ŠÂ¹ : Set
ğ•ŠÂ¹ = Replace preğ•ŠÂ¹

ğ•ŠÂ¹fib : isFibObj ğ•ŠÂ¹
ğ•ŠÂ¹fib = replaceIsFib preğ•ŠÂ¹

base : ğ•ŠÂ¹
base = Î¹ ([ O ]/ Endpoints)

loop : base ~ base
loop = p , refl , cong Î¹ (qeq Endpoints I=O) where
  p : Int â†’ ğ•ŠÂ¹
  p i = Î¹ ([ i ]/ Endpoints)

ğ•ŠÂ¹-elim :
  (P : ğ•ŠÂ¹ â†’ Set)
  (Ï : isFib P)
  (a : P base)
  (l : (i : Int) â†’ P (loop at i))
  (lO : subst P (atO loop) (l O) â‰¡ a)
  (lI : subst P (atI loop) (l I) â‰¡ a)
  â†’ ---------------------
  (x : ğ•ŠÂ¹) â†’ P x
ğ•ŠÂ¹-elim P Ï a l lO lI x = replaceInd preğ•ŠÂ¹ P Ï f x where
  f : (x : preğ•ŠÂ¹) â†’ P (Î¹ x)
  f = qind Endpoints (P âˆ˜ Î¹) l resp where
    resp : (i j : Int) (r : Endpoints i j) â†’
      subst (P âˆ˜ Î¹) (qeq Endpoints r) (l i) â‰¡ l j
    resp .I .O I=O =
      proof:
        subst (P âˆ˜ Î¹) (qeq Endpoints I=O) (l I)
          â‰¡[ substcong P Î¹ (qeq Endpoints I=O) (l I) ]â‰¡
        subst P (cong Î¹ (qeq Endpoints I=O)) (l I)
          â‰¡[ lI ]â‰¡
        a
          â‰¡[ symm lO ]â‰¡
        l O
      qed

-----------------------------
-- Suspension (of an object)
-----------------------------
data preSusp (X : Set) : Set where
  preNorth : preSusp X
  preSouth : preSusp X
  preMerid : X â†’ Int â†’ preSusp X

data MeridEnds {X : Set} : preSusp X â†’ preSusp X â†’ Set where
  meridO : (x : X) â†’ MeridEnds (preMerid x O) preNorth
  meridI : (x : X) â†’ MeridEnds (preMerid x I) preSouth

Susp : Set â†’ Set
Susp X = Replace (preSusp X / MeridEnds)

north : {X : Set} â†’ Susp X
north = Î¹ ([ preNorth ]/ MeridEnds)

south : {X : Set} â†’ Susp X
south = Î¹ ([ preSouth ]/ MeridEnds)

merid : {X : Set} â†’ X â†’ north ~ south
merid {X} x = p , (cong Î¹ (qeq MeridEnds (meridO x))) , cong Î¹ (qeq MeridEnds (meridI x)) where
  p : Int â†’ Susp X
  p i = Î¹ ([ preMerid x i ]/ MeridEnds)

Susp-elim :
  (X  : Set)
  (P  : Susp X â†’ Set)
  (Ï  : isFib P)
  (an : P north)
  (as : P south)
  (al : (x : X)(i : Int) â†’ P (merid x at i))
  (lO : (x : X) â†’ subst P (atO (merid x)) (al x O) â‰¡ an)
  (lI : (x : X) â†’ subst P (atI (merid x)) (al x I) â‰¡ as)
  â†’ ---------------------
  (x : Susp X) â†’ P x
Susp-elim X P Ï an as al lO lI = replaceInd _ P Ï f where
  f : (x : preSusp X / MeridEnds) â†’ P (Î¹ x)
  f = qind MeridEnds (P âˆ˜ Î¹) f' resp where
    f' : (x : preSusp X) â†’ (P âˆ˜ Î¹) ([ x ]/ MeridEnds)
    f' preNorth = an
    f' preSouth = as
    f' (preMerid x i) = al x i
    resp : (x y : preSusp X) (r : MeridEnds x y) â†’
      subst (P âˆ˜ Î¹) (qeq MeridEnds r) (f' x) â‰¡ f' y
    resp .(preMerid x O) .preNorth (meridO x) =
      proof:
        subst (P âˆ˜ Î¹) (qeq MeridEnds (meridO x)) (al x O)
          â‰¡[ substcong P Î¹ (qeq MeridEnds (meridO x)) (al x O) ]â‰¡
        subst P (cong Î¹ (qeq MeridEnds (meridO x))) (al x O)
          â‰¡[ lO x ]â‰¡
        an
      qed
    resp .(preMerid x I) .preSouth (meridI x) =
      proof:
        subst (P âˆ˜ Î¹) (qeq MeridEnds (meridI x)) (al x I)
          â‰¡[ substcong P Î¹ (qeq MeridEnds (meridI x)) (al x I) ]â‰¡
        subst P (cong Î¹ (qeq MeridEnds (meridI x))) (al x I)
          â‰¡[ lI x ]â‰¡
        as
      qed

SuspFunctorial : 
  (X Y : Set)
  (f : X â†’ Y)
  â†’ ---------------------
  Susp X â†’ Susp Y
SuspFunctorial X Y f =
  Susp-elim X (Î» _ â†’ Susp Y) (reindex (Î» _ â†’ Susp Y) (replaceIsFib (preSusp Y / MeridEnds)) (Î» _ â†’ tt))
    north  -- north â†¦ north
    south  -- south â†¦ south
    (Î» x i â†’ (merid (f x)) at i)  -- merid x â†¦ merid (f x)
    (Î» x â†’ proof:
      subst (Î» _ â†’ Susp Y) (atO (merid x)) (merid (f x) at O)
        â‰¡[ substconst (Susp Y) (atO (merid x)) (merid (f x) at O) ]â‰¡
      merid (f x) at O
        â‰¡[ atO (merid (f x)) ]â‰¡
      north
    qed)
    (Î» x â†’ proof:
      subst (Î» _ â†’ Susp Y) (atI (merid x)) (merid (f x) at I)
        â‰¡[ substconst (Susp Y) (atI (merid x)) (merid (f x) at I) ]â‰¡
      merid (f x) at I
        â‰¡[ atI (merid (f x)) ]â‰¡
      south
    qed)

 
